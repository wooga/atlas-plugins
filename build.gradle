/*
 * Copyright 2018-2021 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'gradle.plugin.net.wooga.gradle:atlas-github:2.+'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.14.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:[2,3)'
        classpath 'gradle.plugin.net.wooga.gradle:atlas-GithubReleaseNotes:[0.1, 0.2)'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
        classpath 'gradle.plugin.net.wooga.gradle:atlas-version:0.1.0-rc.8'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0'
    }
}

apply plugin: new GroovyScriptEngine(
        [file('src/main/groovy').absolutePath,
         file('src/main/resources').absolutePath].toArray(new String[2]),
        this.class.classLoader
).loadScriptByName('wooga/gradle/plugins/PluginsPlugin.groovy')

group 'net.wooga.gradle'
description = 'Plugin for wooga gradle plugin development.'


pluginBundle {
    website = 'https://wooga.github.io/atlas-plugins'
    vcsUrl = 'https://github.com/wooga/atlas-plugins'
    tags = ['plugins', 'publishing', 'internal']

    plugins {
        plugins {
            id = 'net.wooga.plugins'
            displayName = 'Wooga Base Gradle Plugin'
            description = 'This plugin provides tasks and basic setup for other plugins'
        }
    }
}

github {
    repositoryName = "wooga/atlas-plugins"
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    implementation 'gradle.plugin.net.wooga.gradle:atlas-github:2.+'
    implementation 'com.gradle.publish:plugin-publish-plugin:0.14.0'
    implementation 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:[2,3)'
    implementation 'gradle.plugin.net.wooga.gradle:atlas-GithubReleaseNotes:[0.1, 0.2)'
    implementation 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
    implementation 'gradle.plugin.net.wooga.gradle:atlas-version:0.1.0-rc.8'
    implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0'
    testImplementation('com.netflix.nebula:nebula-test:[8,9)') {
        version {
            strictly '8.1.0'
        }
    }
    testImplementation('junit:junit:[4,5)')
    testImplementation 'com.wooga.spock.extensions:spock-github-extension:0.1.2'
    testImplementation 'org.ajoberstar.grgit:grgit-core:[4,5)'
}

configurations.all {
    resolutionStrategy {
        force 'org.codehaus.groovy:groovy-all:2.5.12'
        force 'org.codehaus.groovy:groovy-macro:3.0.9'
        force 'org.codehaus.groovy:groovy-nio:2.5.12'
        force 'org.codehaus.groovy:groovy-sql:2.5.12'
        force 'org.codehaus.groovy:groovy-xml:2.5.12'
    }
}
