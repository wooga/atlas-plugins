/*
 * Copyright 2018-2021 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'net.wooga.plugins' version '1.3.0'
    id 'com.gradle.plugin-publish' version '0.14.0'
}

group 'net.wooga.gradle'
description = 'Plugin for wooga gradle plugin development.'

pluginBundle {
    website = 'https://wooga.github.io/atlas-plugins'
    vcsUrl = 'https://github.com/wooga/atlas-plugins'
    tags = ['plugins', 'publishing', 'internal']

    plugins {
        plugins {
            id = 'net.wooga.plugins'
            displayName = 'Wooga Base Gradle Plugin'
            description = 'This plugin provides tasks and basic setup for other plugins'
        }
    }
}

github {
    repositoryName = "wooga/atlas-plugins"
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

/*
This method can be removed after the release of the next major version
 */
private static void setupIntegrationTestConfiguration(Project project) {
    def configurations = project.configurations
    def testImplementation = configurations.getByName("testImplementation")
    def testRuntimeOnly = configurations.getByName("testRuntimeOnly")

    def integrationTestImplementation = configurations.getByName("integrationTestImplementation")
    integrationTestImplementation.extendsFrom(testImplementation)

    def integrationTestRuntimeOnly = configurations.getByName("integrationTestRuntimeOnly")
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

setupIntegrationTestConfiguration(this.project)

dependencies {
    implementation 'gradle.plugin.net.wooga.gradle:atlas-github:[1,2)'
    implementation 'com.netflix.nebula:nebula-release-plugin:[15,16)'
    implementation 'com.gradle.publish:plugin-publish-plugin:0.14.0'
    implementation 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:[2,3)'

    testImplementation('com.netflix.nebula:nebula-test:[8,9)') {
        version {
            strictly '8.1.0'
        }
    }

    testImplementation('junit:junit:[4,5)')
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testImplementation 'org.ajoberstar.grgit:grgit-core:[4,5)'
}
